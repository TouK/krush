buildscript {
    ext {
        kotlin_version = '1.3.60'
        exposed_version = '0.18.1'
        kotlinpoet_version = '1.4.4'
    }

    repositories {
        jcenter()
        mavenLocal()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.60'
    id 'pl.allegro.tech.build.axion-release' version '1.10.2'
    id 'maven-publish'
}

group = 'pl.touk.krush'
project.version = scmVersion.version

sourceCompatibility = 1.8

allprojects {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

configure([project(':annotation-processor'), project(':runtime')]) {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId "com.github.nickgieschen.krush"
                artifactId project.name
                version rootProject.version

                artifact sourcesJar {
                    classifier "sources"
                }
            }
        }

        repositories {
            maven {
                credentials {
                    username = project.properties["deployUsername"]?.toString() ?: System.getenv("DEPLOY_USERNAME")
                    password = project.properties["deployPassword"]?.toString() ?: System.getenv("DEPLOY_PASSWORD")
                }
                def baseUrl = 'https://philanthropist.touk.pl/nexus/content/repositories'
                def releasesRepoUrl = "$baseUrl/releases"
                def snapshotsRepoUrl = "$baseUrl/snapshots"
                url = scmVersion.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }

}
